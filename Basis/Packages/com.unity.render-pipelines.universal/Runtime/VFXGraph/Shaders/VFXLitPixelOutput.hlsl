#ifndef SHADERPASS
#error SHADERPASS must be defined
#endif

#ifndef UNIVERSAL_SHADERPASS_INCLUDED
#error ShaderPass has to be included
#endif


#if (SHADERPASS == SHADERPASS_FORWARD)

float4 VFXCalcPixelOutputForward(const VFX_VARYING_PS_INPUTS i, SurfaceData surfaceData, InputData inputData)
{
#if VFX_MATERIAL_TYPE_SIX_WAY_SMOKE
    URP_LIGHT_ACCUM4 color = UniversalFragmentSixWay(inputData, surfaceData);
#else
    #if defined(_DBUFFER) && !defined(_SURFACE_TYPE_TRANSPARENT)
        ApplyDecalToSurfaceData(i.VFX_VARYING_POSCS, surfaceData, inputData);
    #endif
    URP_LIGHT_ACCUM4 color = UniversalFragmentPBR(inputData, surfaceData);
#endif

    color.rgb = ClampExposed(inputData.preExposureMultiplier * BlendDistanceFog(color.rgb, i.VFX_VARYING_POSCS));
    color = VFXApplyVolumetricFog(color, i.VFX_VARYING_POSCS);

#if IS_OPAQUE_PARTICLE
    bool isSurfaceTypeTransparent = false;
#else
    bool isSurfaceTypeTransparent = true;
#endif
    color.a = OutputAlpha(color.a, isSurfaceTypeTransparent);
    return color;
}

#ifndef VFX_SHADERGRAPH

#if VFX_MATERIAL_TYPE_SIX_WAY_SMOKE
#define SurfaceData SixWaySurfaceData
#endif

float4 VFXGetPixelOutputForward(const VFX_VARYING_PS_INPUTS i, float3 normalWS, const VFXUVData uvData, bool frontFace)
{
    SurfaceData surfaceData;
    InputData inputData;

    VFXGetURPLitData(surfaceData, inputData, i, normalWS, uvData, frontFace, (uint2)0);
    return VFXCalcPixelOutputForward(i, surfaceData, inputData);
}

#else

float4 VFXGetPixelOutputForwardShaderGraph(const VFX_VARYING_PS_INPUTS i, SurfaceData surfaceData, float3 normalWS)
{
    float3 posRWS = VFXGetPositionRWS(i);
    float4 posSS = i.VFX_VARYING_POSCS;
    PositionInputs posInput = GetPositionInput(posSS.xy, _ScreenSize.zw, posSS.z, posSS.w, posRWS, (uint2)0);

    VFXUVData uvData = (VFXUVData)0;
    InputData inputData = VFXGetInputData(i, posInput, normalWS, true);

    return VFXCalcPixelOutputForward(i, surfaceData, inputData);
}
#endif

#elif (SHADERPASS == SHADERPASS_GBUFFER)

#ifndef VFX_SHADERGRAPH
void VFXComputePixelOutputToGBuffer(const VFX_VARYING_PS_INPUTS i, const float3 normalWS, const VFXUVData uvData, out GBufferFragOutput gBuffer)
{
    SurfaceData surfaceData;
    InputData inputData;
    VFXGetURPLitData(surfaceData, inputData, i, normalWS, uvData, true, (uint2)0);

    BRDFData brdfData;
    InitializeBRDFData(surfaceData.albedo, surfaceData.metallic, surfaceData.specular, surfaceData.smoothness, surfaceData.alpha, brdfData);

    URP_LIGHT_ACCUM3 color = GlobalIllumination(brdfData, (BRDFData)0, 0,
                                             inputData.bakedGI, surfaceData.occlusion, inputData.positionWS,
                                             inputData.normalWS, inputData.viewDirectionWS, inputData.normalizedScreenSpaceUV);

#if defined(_DBUFFER) && !defined(_SURFACE_TYPE_TRANSPARENT)
    ApplyDecalToBaseColor(i.VFX_VARYING_POSCS, surfaceData.albedo);
#endif

    gBuffer = PackGBuffersBRDFData(brdfData, inputData, surfaceData.smoothness, ClampExposed(inputData.preExposureMultiplier * (surfaceData.emission + color)), surfaceData.occlusion);
}

#else
void VFXComputePixelOutputToGBufferShaderGraph(const VFX_VARYING_PS_INPUTS i, SurfaceData surfaceData, const float3 normalWS, out GBufferFragOutput gBuffer)
{
    float3 posRWS = VFXGetPositionRWS(i);
    float4 posSS = i.VFX_VARYING_POSCS;
    PositionInputs posInput = GetPositionInput(posSS.xy, _ScreenSize.zw, posSS.z, posSS.w, posRWS, (uint2)0);

    VFXUVData uvData = (VFXUVData)0;
    InputData inputData = VFXGetInputData(i, posInput, normalWS, true);

    BRDFData brdfData;
    InitializeBRDFData(surfaceData.albedo, surfaceData.metallic, surfaceData.specular, surfaceData.smoothness, surfaceData.alpha, brdfData);

    URP_LIGHT_ACCUM3 color = GlobalIllumination(brdfData, (BRDFData)0, 0,
                                                 inputData.bakedGI, surfaceData.occlusion, inputData.positionWS,
                                                 inputData.normalWS, inputData.viewDirectionWS, inputData.normalizedScreenSpaceUV);

#if defined(_DBUFFER) && !defined(_SURFACE_TYPE_TRANSPARENT)
    ApplyDecalToBaseColor(i.VFX_VARYING_POSCS, surfaceData.albedo);
#endif

    gBuffer = PackGBuffersBRDFData(brdfData, inputData, surfaceData.smoothness, ClampExposed(inputData.preExposureMultiplier * (surfaceData.emission + color)), surfaceData.occlusion);
}

#endif
#endif
